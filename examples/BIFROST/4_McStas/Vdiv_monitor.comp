/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright (C) 1997-2011, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: Vdiv_monitor
*
*%I
* Written by: KL,
* Date: Nov. 11, 1998
* Version: $Revision: 1.13 $
* Origin: Risoe
* Release: McStas 1.12c
* Modified by: Thomas Hansen,  December, 1999
*
* A divergence sensitive monitor.
*
*%D
* A divergence sensitive monitor. The counts are distributed in
* n pixels.
*
* Example: Hdiv_monitor(xmin=-0.1, xmax=0.1, ymin=-0.1, ymax=0.1,
*           nv=20, filename="Output.hd", v_maxdiv=2)
*
*%P
* INPUT PARAMETERS:
*
* xmin:     Lower x bound of detector opening (m)
* xmax:     Upper x bound of detector opening (m)
* ymin:     Lower y bound of detector opening (m)
* ymax:     Upper y bound of detector opening (m)
* xwidth:   Width/diameter of detector (x). Overrides xmin,xmax. (m)
* yheight:  Height of detector (y). Overrides ymin,ymax. (m)
* nv:       Number of pixel rows (1)
* V_maxdiv  Maximal vertical divergence detected (degrees)
* filename: Name of file in which to store the detector image (text)
* restore_neutron: If set, the monitor does not influence the neutron state (1)
*
* OUTPUT PARAMETERS:
*
* Div_N:    Array of neutron counts
* Div_p:    Array of neutron weight counts
* Div_p2:   Array of second moments
*
*%E
*/


DEFINE COMPONENT Vdiv_monitor
DEFINITION PARAMETERS (nv=20, string filename, restore_neutron=0)
SETTING PARAMETERS (xmin=0, xmax=0, ymin=0, ymax=0, xwidth=0, yheight=0,
                       v_maxdiv=2 )
OUTPUT PARAMETERS (Div_N, Div_p, Div_p2)
//POLARISATION PARAMETERS (sx,sy,sz)
DECLARE
  %{
    double Div_N[nv];
    double Div_p[nv];
    double Div_p2[nv];
  %}
INITIALIZE
  %{
    int i;

    if (xwidth  > 0) { xmax = xwidth/2;  xmin = -xmax; }
    if (yheight > 0) { ymax = yheight/2; ymin = -ymax; }

    if ((xmin >= xmax) || (ymin >= ymax)) {
            printf("Vdiv_monitor: %s: Null detection area !\n"
                   "ERROR         (xwidth,yheight,xmin,xmax,ymin,ymax). Exiting",
           NAME_CURRENT_COMP);
      exit(0);
    }

    for (i=0; i<nv; i++)
     {
/*       printf("HDiv_monitor: %d\n",i); */
      Div_N[i] = 0;
      Div_p[i] = 0;
      Div_p2[i] = 0;
     }
/*     printf("%d %d %d\n",i,nv,v_maxdiv); */
  %}
TRACE
  %{
    int i;
    double v_div;

    PROP_Z0;
    if (x>xmin && x<xmax && y>ymin && y<ymax)
    {
      v_div = RAD2DEG*atan2(vy,vz);
      if (v_div < (double)v_maxdiv && v_div > -(double)v_maxdiv)
      {
        i = floor((v_div + (double)v_maxdiv)*nv/(2.0*(double)v_maxdiv));
        Div_N[i]++;
        Div_p[i] += p;
        Div_p2[i] += p*p;
        SCATTER;
      }
    }
    if (restore_neutron) {
      RESTORE_NEUTRON(INDEX_CURRENT_COMP, x, y, z, vx, vy, vz, t, sx, sy, sz, p);
    }
  %}
SAVE
  %{
    DETECTOR_OUT_1D(
        "vertical divergence monitor",
        "vertical divergence [deg]",
        "Intensity",
        "divergence", -v_maxdiv, v_maxdiv, nv,
        &Div_N[0],&Div_p[0],&Div_p2[0],
        filename);
  %}

MCDISPLAY
  %{
    magnify("xy");
    multiline(5, (double)xmin, (double)ymin, 0.0,
                 (double)xmax, (double)ymin, 0.0,
                 (double)xmax, (double)ymax, 0.0,
                 (double)xmin, (double)ymax, 0.0,
                 (double)xmin, (double)ymin, 0.0);
%}


END
